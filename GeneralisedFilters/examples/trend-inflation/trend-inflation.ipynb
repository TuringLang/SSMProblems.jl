{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Trend Inflation\n",
    "\n",
    "[![Open in Colab](https://colab.research.google.com/assets/colab-badge.svg)](https://colab.research.google.com/github/TuringLang/SSMProblems.jl/blob/main/GeneralisedFilters/examples/trend-inflation/trend-inflation.ipynb) [![View Source](https://img.shields.io/badge/View%20Source-GitHub-181717?logo=github)](https://github.com/TuringLang/SSMProblems.jl/blob/main/GeneralisedFilters/examples/trend-inflation/trend-inflation.ipynb) [![Example Page](https://img.shields.io/badge/Example%20Page-Docs-0A7F2E)](https://turinglang.org/SSMProblems.jl/GeneralisedFilters/dev/examples/trend-inflation/)\n",
    "\n",
    "This example is a replication of the univariate state space model suggested by (Stock &\n",
    "Watson, 2016) using GeneralisedFilters to define a heirarchical model for use in Rao-Blackwellised particle filtering."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg\n",
    "\n",
    "Pkg.activate(\".\")\n",
    "if isfile(\"Project.toml\")\n",
    "    Pkg.instantiate()\n",
    "else\n",
    "    Pkg.add([\n",
    "        \"SSMProblems\",\n",
    "        \"GeneralisedFilters\",\n",
    "        \"CSV\",\n",
    "        \"CairoMakie\",\n",
    "        \"DataFrames\",\n",
    "        \"Distributions\",\n",
    "        \"LogExpFunctions\",\n",
    "        \"PDMats\",\n",
    "        \"StatsBase\",\n",
    "    ])\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using GeneralisedFilters\n",
    "using SSMProblems\n",
    "using Distributions\n",
    "using Random\n",
    "using StatsBase\n",
    "using LinearAlgebra\n",
    "using PDMats\n",
    "\n",
    "const GF = GeneralisedFilters"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Downloads\n",
    "\n",
    "const UTILITIES_URL = \"https://raw.githubusercontent.com/TuringLang/SSMProblems.jl/main/GeneralisedFilters/examples/trend-inflation/utilities.jl\"\n",
    "const UTILITIES_PATH = isfile(\"utilities.jl\") ? \"utilities.jl\" : Downloads.download(UTILITIES_URL)\n",
    "include(UTILITIES_PATH);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Model Definition"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We begin by defining the local level trend model, a linear Gaussian model with a weakly\n",
    "stationary random walk component. The dynamics of which are as follows:"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "$$\n",
    "\\begin{aligned}\n",
    "    y_{t} &= x_{t} + \\eta_{t} \\\\\n",
    "    x_{t+1} &= x_{t} + \\varepsilon_{t}\n",
    "\\end{aligned}\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "However, this model is not enough to capture trend dynamics when faced with structural\n",
    "breaks. (Stock & Watson, 2007) suggest adding a stochastic volatiltiy component, defined\n",
    "like so:"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "$$\n",
    "\\begin{aligned}\n",
    "    \\log \\sigma_{\\eta, t+1} = \\log \\sigma_{\\eta, t} + \\nu_{\\eta, t} \\\\\n",
    "    \\log \\sigma_{\\varepsilon, t+1} = \\log \\sigma_{\\varepsilon, t} + \\nu_{\\varepsilon, t}\n",
    "\\end{aligned}\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "where $\\nu_{z,t} \\sim N(0, \\gamma)$ for $z \\in \\{ \\varepsilon, \\eta \\}$."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Using `GeneralisedFilters`, we can construct a heirarchical version of this model such\n",
    "that the local level trend component is conditionally linear Gaussian on the volatility\n",
    "draws."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Stochastic Volatility Process"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We begin by defining the non-linear dynamics, which aren't conditioned contemporaneous\n",
    "states. Since these processes are traditionally non-linear/non-Gaussian we use the\n",
    "SSMProblems interface to define the stochastic volatility components."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct StochasticVolatilityPrior{T<:Real} <: StatePrior end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function SSMProblems.distribution(prior::StochasticVolatilityPrior{T}; kwargs...) where {T}\n",
    "    return product_distribution(Normal(zero(T), T(1)), Normal(zero(T), T(1)))\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "For the dynamics, instead of using the `SSMProblems.distribution` utility, we only define\n",
    "the `simulate` method, which is sufficient for the RBPF."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct StochasticVolatility{ΓT<:AbstractVector} <: LatentDynamics\n",
    "    γ::ΓT\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function SSMProblems.simulate(\n",
    "    rng::AbstractRNG,\n",
    "    proc::StochasticVolatility,\n",
    "    step::Integer,\n",
    "    state::AbstractVector{T};\n",
    "    kwargs...,\n",
    ") where {T<:Real}\n",
    "    new_state = deepcopy(state)\n",
    "    new_state[1:2] += proc.γ .* randn(rng, T, 2)\n",
    "    return new_state\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Local Level Trend Process\n",
    "\n",
    "For the conditionally linear and Gaussian components, we subtype the model and provide a\n",
    "keyword argument as the conditional element. In this case $A$ and $b$ remain constant, but\n",
    "$Q$ is conditional on the log variance, stored in `new_outer` (the nomenclature chosen for\n",
    "heirarchical modeling)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct LocalLevelTrend <: LinearGaussianLatentDynamics end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "GF.calc_A(::LocalLevelTrend, ::Integer; kwargs...) = [1;;]\n",
    "GF.calc_b(::LocalLevelTrend, ::Integer; kwargs...) = [0;]\n",
    "function GF.calc_Q(::LocalLevelTrend, ::Integer; new_outer, kwargs...)\n",
    "    return PDMat([exp(new_outer[1]);;])\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Similarly, we define the observation process conditional on a separate log variance."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct SimpleObservation <: LinearGaussianObservationProcess end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "GF.calc_H(::SimpleObservation, ::Integer; kwargs...) = [1;;]\n",
    "GF.calc_c(::SimpleObservation, ::Integer; kwargs...) = [0;]\n",
    "function GF.calc_R(::SimpleObservation, ::Integer; new_outer, kwargs...)\n",
    "    return PDMat([exp(new_outer[2]);;])\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Unobserved Components with Stochastic Volatility"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The state space model suggested by (Stock & Watson, 2007) can be constructed with the\n",
    "following method:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function UCSV(γ::T) where {T<:Real}\n",
    "    stoch_vol_prior = StochasticVolatilityPrior{T}()\n",
    "    stoch_vol_process = StochasticVolatility(fill(γ, 2))\n",
    "\n",
    "    local_level_model = StateSpaceModel(\n",
    "        GF.HomogeneousGaussianPrior(zeros(T, 1), PDMat([100.0;;])),\n",
    "        LocalLevelTrend(),\n",
    "        SimpleObservation(),\n",
    "    )\n",
    "\n",
    "    return HierarchicalSSM(stoch_vol_prior, stoch_vol_process, local_level_model)\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "For plotting, we can extract the ancestry of the Rao Blackwellised particles using the\n",
    "callback system. For our inflation data, this reduces to the following:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "rng = MersenneTwister(1234);\n",
    "sparse_ancestry = GF.AncestorCallback(nothing);\n",
    "states, ll = GF.filter(\n",
    "    rng,\n",
    "    UCSV(0.2),\n",
    "    RBPF(BF(2^12), KalmanFilter()),\n",
    "    [[pce] for pce in fred_data.value];\n",
    "    callback=sparse_ancestry,\n",
    ");"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The `sparse_ancestry` object stores a sparse ancestry tree which we can use to approximate\n",
    "the smoothed series without an additional backwards pass. We can convert this data\n",
    "structure to a human readable array by using `GeneralisedFilters.get_ancestry` and then\n",
    "take the mean path by passing a custom function."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "trends, volatilities = mean_path(GF.get_ancestry(sparse_ancestry.tree), states);\n",
    "plot_ucsv(trends[1, :], eachrow(volatilities), fred_data)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Outlier Adjustments"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "For additional robustness, (Stock & Watson, 2016) account for one-time measurement shocks\n",
    "and suggest an alteration in the observation equation, where"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "$$\n",
    "\\eta_{t} \\sim N(0, s_{t} \\cdot \\sigma_{\\eta, t}^2) \\quad \\quad s_{t} \\sim \\begin{cases}\n",
    "U(0,2) & \\text{ with probability } p \\\\\n",
    "\\delta(1) & \\text{ with probability } 1 - p\n",
    "\\end{cases}\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The prior is the same as before, but with additional state which we can assume will always\n",
    "be 1; using the `Distributions` interface this is just `Dirac(1)`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct OutlierAdjustedVolatilityPrior{T<:Real} <: StatePrior end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function SSMProblems.distribution(\n",
    "    prior::OutlierAdjustedVolatilityPrior{T}; kwargs...\n",
    ") where {T}\n",
    "    return product_distribution(Normal(zero(T), T(1)), Normal(zero(T), T(1)), Dirac(one(T)))\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "In terms of the model definition, we can construct a separate `LatentDynamics` which\n",
    "contains the same volatility process as before, but with the respective draw in the third\n",
    "component."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct OutlierAdjustedVolatility{ΓT} <: LatentDynamics\n",
    "    volatility::StochasticVolatility{ΓT}\n",
    "    switch_dist::Bernoulli\n",
    "    outlier_dist::Uniform\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The simulation then calls the volatility process, and computes the outlier term in the\n",
    "third state"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function SSMProblems.simulate(\n",
    "    rng::AbstractRNG,\n",
    "    proc::OutlierAdjustedVolatility,\n",
    "    step::Integer,\n",
    "    state::AbstractVector{T};\n",
    "    kwargs...,\n",
    ") where {T<:Real}\n",
    "    new_state = SSMProblems.simulate(rng, proc.volatility, step, state; kwargs...)\n",
    "    new_state[3] = rand(rng, proc.switch_dist) ? rand(rng, proc.outlier_dist) : one(T)\n",
    "    return new_state\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "For the observation process, we define a new object where $R$ is dependent on both the\n",
    "measurement volatility as well as this outlier adjustment coefficient."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct OutlierAdjustedObservation <: LinearGaussianObservationProcess end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "GF.calc_H(::OutlierAdjustedObservation, ::Integer; kwargs...) = [1;;]\n",
    "GF.calc_c(::OutlierAdjustedObservation, ::Integer; kwargs...) = [0;]\n",
    "function GF.calc_R(::OutlierAdjustedObservation, ::Integer; new_outer, kwargs...)\n",
    "    return PDMat([new_outer[3] * exp(new_outer[2]);;])\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Outlier Adjusted UCSV"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The state space model suggested by (Stock & Watson, 2007) can be constructed with the\n",
    "following method:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function UCSVO(γ::T, prob::T) where {T<:Real}\n",
    "    stoch_vol_prior = OutlierAdjustedVolatilityPrior{T}()\n",
    "    stoch_vol_process = OutlierAdjustedVolatility(\n",
    "        StochasticVolatility(fill(γ, 2)), Bernoulli(prob), Uniform{T}(2, 10)\n",
    "    )\n",
    "\n",
    "    local_level_model = StateSpaceModel(\n",
    "        GF.HomogeneousGaussianPrior(zeros(T, 1), PDMat([100.0;;])),\n",
    "        LocalLevelTrend(),\n",
    "        OutlierAdjustedObservation(),\n",
    "    )\n",
    "\n",
    "    return HierarchicalSSM(stoch_vol_prior, stoch_vol_process, local_level_model)\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We then repeat the same experiment, this time with an outlier probability of $p = 0.05$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "rng = MersenneTwister(1234);\n",
    "sparse_ancestry = GF.AncestorCallback(nothing)\n",
    "states, ll = GF.filter(\n",
    "    rng,\n",
    "    UCSVO(0.2, 0.05),\n",
    "    RBPF(BF(2^12), KalmanFilter()),\n",
    "    [[pce] for pce in fred_data.value];\n",
    "    callback=sparse_ancestry,\n",
    ");"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "this process is identical to the last, except with an additional `volatilities` state\n",
    "which captures the outlier distance. We omit this feature in the plots, but the impact is\n",
    "clear when comparing the maximum transitory noise around the GFC."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "trends, volatilities = mean_path(GF.get_ancestry(sparse_ancestry.tree), states);\n",
    "p = plot_ucsv(trends[1, :], eachrow(volatilities), fred_data)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.3",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 3
}
